buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

repositories {
    maven { url 'http://dl.bintray.com/populov/maven' }
    maven { url 'https://github.com/kageiit/maven-repo/raw/master' }
    maven { url "https://github.com/kolavar/android-support-v4-preferencefragment/raw/master/maven-repository/" }
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    signingConfigs {
        playStore
    }
    compileSdkVersion 22
    buildToolsVersion '22.0.1'
    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 22
        versionCode 41
        versionName "3.8.1"
    }

    buildTypes {
        debug {
            zipAlignEnabled true
        }
        release {
            zipAlignEnabled true
            signingConfig signingConfigs.playStore
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')

    compile 'com.android.support:support-v4:22.1.1'
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'com.android.support:cardview-v7:22.1.1'
    compile 'com.larswerkman:HoloColorPicker:1.4@aar'
    compile 'com.github.johnkil.android-progressfragment:progressfragment:1.4.0@aar'
    compile 'com.android.support:support-v4-preferencefragment:1.0.0@aar'
    compile 'org.apmem.tools:layouts:1.9@aar'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.squareup.okhttp:okhttp:1.5.4'
    compile 'org.ocpsoft.prettytime:prettytime:3.2.4.Final'
    compile 'com.shamanland:fab:0.0.6'
    compile 'com.github.castorflex.smoothprogressbar:library:1.0.0'
    compile 'com.nineoldandroids:library:2.4.0+'
    compile('com.crashlytics.sdk.android:crashlytics:2.4.0@aar') {
        transitive = true;
    }
}

def Properties props = new Properties()
def propFile = new File('signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))
}

if (props.containsKey('STORE_FILE')
        && props.containsKey('STORE_PASSWORD')
        && props.containsKey('KEY_ALIAS')
        && props.containsKey('KEY_PASSWORD')) {
    android.signingConfigs.playStore.storeFile = file(props['STORE_FILE'])
    android.signingConfigs.playStore.storePassword = props['STORE_PASSWORD']
    android.signingConfigs.playStore.keyAlias = props['KEY_ALIAS']
    android.signingConfigs.playStore.keyPassword = props['KEY_PASSWORD']
} else {
    println 'signing.properties not found or incomplete'
    android.buildTypes.release.signingConfig = null
}
