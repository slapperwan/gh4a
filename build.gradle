buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.3'
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

repositories {
    maven { url 'http://dl.bintray.com/populov/maven' }
    maven { url 'https://github.com/kageiit/maven-repo/raw/master' }
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    signingConfigs {
        playStore
    }
    compileSdkVersion 24
    buildToolsVersion '24.0.0'
    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 23
        versionCode 47
        versionName "4.0.4"
    }

    buildTypes {
        debug {
            zipAlignEnabled true
        }
        release {
            zipAlignEnabled true
            signingConfig signingConfigs.playStore
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')

    compile 'com.android.support:support-v4:24.1.1'
    compile 'com.android.support:appcompat-v7:24.1.1'
    compile 'com.android.support:cardview-v7:24.1.1'
    compile 'com.android.support:preference-v7:24.1.1'
    compile 'com.android.support:preference-v14:24.1.1'
    compile 'com.android.support:design:24.1.1'
    compile 'com.larswerkman:HoloColorPicker:1.5@aar'
    compile 'com.github.johnkil.android-progressfragment:progressfragment:1.4.0@aar'
    compile 'com.caverock:androidsvg:1.2.2-beta-1@aar'
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.1.+'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.squareup.okhttp:okhttp:1.6.0'
    compile 'org.ocpsoft.prettytime:prettytime:3.0.0.Final'
    compile 'com.github.castorflex.smoothprogressbar:library:1.1.0'
    compile 'com.nineoldandroids:library:2.4.0+'
    compile('com.crashlytics.sdk.android:crashlytics:2.4.0@aar') {
        transitive = true;
    }
}

def Properties props = new Properties()
def propFile = new File('signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))
}

if (props.containsKey('STORE_FILE')
        && props.containsKey('STORE_PASSWORD')
        && props.containsKey('KEY_ALIAS')
        && props.containsKey('KEY_PASSWORD')) {
    android.signingConfigs.playStore.storeFile = file(props['STORE_FILE'])
    android.signingConfigs.playStore.storePassword = props['STORE_PASSWORD']
    android.signingConfigs.playStore.keyAlias = props['KEY_ALIAS']
    android.signingConfigs.playStore.keyPassword = props['KEY_PASSWORD']
} else {
    println 'signing.properties not found or incomplete'
    android.buildTypes.release.signingConfig = null
}
