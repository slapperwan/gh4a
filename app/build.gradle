def taskRequests = getGradle().getStartParameter().getTaskRequests().toString()
def isPlaystore = taskRequests.contains("Playstore") || taskRequests.contains("playstore")

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.22.2'
    }
}

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
}

apply plugin: 'com.android.application'

if (isPlaystore) {
    apply plugin: 'io.fabric'
}

android {
    signingConfigs {
        playStore
    }
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 53
        versionName "4.1.2"
    }

    buildTypes {
        debug {
            zipAlignEnabled true
        }
        release {
            zipAlignEnabled true
            signingConfig signingConfigs.playStore
        }
        buildTypes.each {
            it.buildConfigField 'String', 'CLIENT_ID', ClientId
            it.buildConfigField 'String', 'CLIENT_SECRET', ClientSecret
        }
    }

    productFlavors {
        // includes proprietary libs
        playstore {

        }

        // only foss
        foss {

        }
    }
}

ext {
    supportLibraryVersion = '25.3.1'
}

dependencies {
    compile project(':github-api')
    compile "com.android.support:support-v4:$supportLibraryVersion"
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:cardview-v7:$supportLibraryVersion"
    compile "com.android.support:preference-v7:$supportLibraryVersion"
    compile "com.android.support:preference-v14:$supportLibraryVersion"
    compile "com.android.support:customtabs:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile 'com.larswerkman:HoloColorPicker:1.5@aar'
    compile 'com.caverock:androidsvg:1.2.2-beta-1@aar'
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.1.17'
    compile 'org.ocpsoft.prettytime:prettytime:4.0.1.Final'
    compile 'com.github.castorflex.smoothprogressbar:library:1.1.0'
    compile 'org.ccil.cowan.tagsoup:tagsoup:1.2.1'
    compile 'com.github.pluscubed:recycler-fast-scroll:0.3.2@aar'
    compile('com.vdurmont:emoji-java:3.2.0') {
        exclude group: 'org.json', module : 'json'
    }
    playstoreCompile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }
}

def Properties props = new Properties()
def propFile = new File('signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))
}

if (props.containsKey('STORE_FILE')
        && props.containsKey('STORE_PASSWORD')
        && props.containsKey('KEY_ALIAS')
        && props.containsKey('KEY_PASSWORD')) {
    android.signingConfigs.playStore.storeFile = file(props['STORE_FILE'])
    android.signingConfigs.playStore.storePassword = props['STORE_PASSWORD']
    android.signingConfigs.playStore.keyAlias = props['KEY_ALIAS']
    android.signingConfigs.playStore.keyPassword = props['KEY_PASSWORD']
} else {
    println 'signing.properties not found or incomplete'
    android.buildTypes.release.signingConfig = null
}
