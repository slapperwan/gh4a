def taskRequests = getGradle().getStartParameter().getTaskRequests().toString()
def isPlaystore = taskRequests.contains("Playstore") || taskRequests.contains("playstore")

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
    google()
}

apply plugin: 'com.android.application'

if (isPlaystore) {
    apply plugin: 'io.fabric'
}

android {
    signingConfigs {
        playStore
    }
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 56
        versionName "4.2.2"
        applicationId "com.gh4a"

        buildConfigField 'String', 'CLIENT_ID', ClientId
        buildConfigField 'String', 'CLIENT_SECRET', ClientSecret
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            zipAlignEnabled true
        }
        release {
            zipAlignEnabled true
            signingConfig signingConfigs.playStore
        }
    }

    flavorDimensions "type"

    productFlavors {
        // includes proprietary libs
        playstore {
            dimension "type"
        }

        // only foss
        foss {
            dimension "type"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'com.android.support:support-v4:26.1.0'
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:cardview-v7:26.1.0'
    implementation 'com.android.support:preference-v7:26.1.0'
    implementation 'com.android.support:preference-v14:26.1.0'
    implementation 'com.android.support:customtabs:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.github.maniac103:githubsdk:gh4a-SNAPSHOT'
    implementation 'com.larswerkman:HoloColorPicker:1.5@aar'
    implementation 'com.caverock:androidsvg:1.2.2-beta-1@aar'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.1.+'
    implementation 'org.ocpsoft.prettytime:prettytime:4.0.1.Final'
    implementation 'com.github.castorflex.smoothprogressbar:library:1.1.0'
    implementation 'org.ccil.cowan.tagsoup:tagsoup:1.2.1'
    implementation 'com.github.pluscubed:recycler-fast-scroll:0.3.2@aar'
    implementation('com.vdurmont:emoji-java:3.2.0') {
        exclude group: 'org.json', module : 'json'
    }
    implementation 'com.github.Tunous:MarkdownEdit:0.1.1'
    implementation 'com.evernote:android-job:1.2.0-RC4'
    playstoreImplementation('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true
    }
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.4.2'
}

def props = new Properties()
def propFile = new File('signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))
}

if (props.containsKey('STORE_FILE')
        && props.containsKey('STORE_PASSWORD')
        && props.containsKey('KEY_ALIAS')
        && props.containsKey('KEY_PASSWORD')) {
    android.signingConfigs.playStore.storeFile = file(props['STORE_FILE'])
    android.signingConfigs.playStore.storePassword = props['STORE_PASSWORD']
    android.signingConfigs.playStore.keyAlias = props['KEY_ALIAS']
    android.signingConfigs.playStore.keyPassword = props['KEY_PASSWORD']
} else {
    println 'signing.properties not found or incomplete'
    android.buildTypes.release.signingConfig = null
}
