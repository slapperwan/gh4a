def taskRequests = getGradle().getStartParameter().getTaskRequests().toString()
def isPlaystore = taskRequests.contains("Playstore") || taskRequests.contains("playstore")

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
    maven { url "https://maven.google.com" }
}

apply plugin: 'com.android.application'

if (isPlaystore) {
    apply plugin: 'io.fabric'
}

android {
    signingConfigs {
        playStore
    }
    compileSdkVersion 26
    buildToolsVersion '26.0.1'
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 56
        versionName "4.2.2"

        buildConfigField 'String', 'CLIENT_ID', ClientId
        buildConfigField 'String', 'CLIENT_SECRET', ClientSecret
    }

    buildTypes {
        debug {
            zipAlignEnabled true
        }
        release {
            zipAlignEnabled true
            signingConfig signingConfigs.playStore
        }
    }

    productFlavors {
        // includes proprietary libs
        playstore {

        }

        // only foss
        foss {

        }
    }
}

dependencies {
    compile project(':github-api')
    compile 'com.android.support:support-v4:26.0.1'
    compile 'com.android.support:appcompat-v7:26.0.1'
    compile 'com.android.support:cardview-v7:26.0.1'
    compile 'com.android.support:preference-v7:26.0.1'
    compile 'com.android.support:preference-v14:26.0.1'
    compile 'com.android.support:customtabs:26.0.1'
    compile 'com.android.support:design:26.0.1'
    compile 'com.larswerkman:HoloColorPicker:1.5@aar'
    compile 'com.caverock:androidsvg:1.2.2-beta-1@aar'
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.1.+'
    compile 'org.ocpsoft.prettytime:prettytime:4.0.1.Final'
    compile 'com.github.castorflex.smoothprogressbar:library:1.1.0'
    compile 'org.ccil.cowan.tagsoup:tagsoup:1.2.1'
    compile 'com.github.pluscubed:recycler-fast-scroll:0.3.2@aar'
    compile('com.vdurmont:emoji-java:3.2.0') {
        exclude group: 'org.json', module : 'json'
    }
    compile 'com.github.Tunous:MarkdownEdit:0.1.1'
    playstoreCompile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.4.2'
}

def Properties props = new Properties()
def propFile = new File('signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))
}

if (props.containsKey('STORE_FILE')
        && props.containsKey('STORE_PASSWORD')
        && props.containsKey('KEY_ALIAS')
        && props.containsKey('KEY_PASSWORD')) {
    android.signingConfigs.playStore.storeFile = file(props['STORE_FILE'])
    android.signingConfigs.playStore.storePassword = props['STORE_PASSWORD']
    android.signingConfigs.playStore.keyAlias = props['KEY_ALIAS']
    android.signingConfigs.playStore.keyPassword = props['KEY_PASSWORD']
} else {
    println 'signing.properties not found or incomplete'
    android.buildTypes.release.signingConfig = null
}
