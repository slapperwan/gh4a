repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    google()
}

apply plugin: 'com.android.application'

def clientProps = new Properties()
def clientPropFile = new File('client.properties')
if (clientPropFile.canRead()) {
    clientProps.load(new FileInputStream(clientPropFile))
}

ext {
    if (clientProps.containsKey('ClientId')
            && clientProps.containsKey('ClientSecret')) {
        ClientId = clientProps['ClientId']
        ClientSecret = clientProps['ClientSecret']
    } else {
        println 'client.properties not found or incomplete'
    }
}

android {
    compileSdk 36
    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 77
        versionName "4.6.14"
        applicationId "com.gh4a"
        resourceConfigurations += ['en']

        buildConfigField 'String', 'CLIENT_ID', ClientId
        buildConfigField 'String', 'CLIENT_SECRET', ClientSecret
    }

    signingConfigs {
        release
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
    namespace 'com.gh4a'
    buildFeatures {
        buildConfig true
    }

    dependenciesInfo {
        includeInApk false
        includeInBundle false
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'

    def okHttpVersion = '4.12.0'
    def retrofitVersion = '3.0.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.browser:browser:1.9.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.print:print:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.work:work-runtime:2.10.5'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation("com.squareup.retrofit2:converter-simplexml:$retrofitVersion") {
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
        exclude group: 'xpp3', module: 'xpp3'
    }
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    implementation 'com.github.maniac103:rxloader:master-SNAPSHOT'
    implementation 'com.github.maniac103:githubsdk:0.7.0.22'
    implementation 'com.larswerkman:HoloColorPicker:1.5@aar'
    implementation 'com.caverock:androidsvg-aar:1.4'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.29'
    implementation 'org.ocpsoft.prettytime:prettytime:5.0.9.Final'
    implementation 'com.github.castorflex.smoothprogressbar:library:1.3.0'
    implementation 'org.ccil.cowan.tagsoup:tagsoup:1.2.1'
    implementation 'com.github.pluscubed:recycler-fast-scroll:3de76812553a77bfd25d3aea0a0af4d96516c3e3@aar'
    implementation('com.vdurmont:emoji-java:5.1.1') {
        exclude group: 'org.json', module : 'json'
    }
    implementation 'com.github.Tunous:MarkdownEdit:1.0.0'
    implementation 'com.github.ilyagulya:traceur:update-rx-2.2.1'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.16'
}

def props = new Properties()
def propFile = new File('signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))
}

if (props.containsKey('STORE_FILE')
        && props.containsKey('STORE_PASSWORD')
        && props.containsKey('KEY_ALIAS')
        && props.containsKey('KEY_PASSWORD')) {
    android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
    android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
    android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
    android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
} else {
    println 'signing.properties not found or incomplete'
    android.buildTypes.release.signingConfig = null
}
